Author: Saravanan Balachandran
Created: 2025-11-14
Data Sources: DeviceProcessEvents, DeviceFileEvents, DeviceEvents
Platform: Windows (Microsoft Defender for Endpoint â€“ MDE)


Technique ID	Technique Name	Tactic
T1059.007	JavaScript Execution	Execution
T1059.001	PowerShell	Execution
T1082	System Information Discovery	Discovery
T1057	Process Discovery	Discovery
T1547.001	Startup Folder / Scheduled Task	Persistence
T1053.002	Scheduled Task	Persistence


Description

This detection identifies Gootloader / Vanilla Tempest infection flow by correlating execution and persistence behaviors on the same device within 24 hours. Gootloader is currently active in the wild and frequently bypasses traditional AV by delivering JavaScript-based loaders through SEO poisoning. It ultimately leads to PowerShell reconnaissance, persistence via scheduled tasks and Startup LNK files, and deployment of additional malware such as the Supper backdoor.A recent observation on a Kantar Media MISMAN device showed scheduled task execution of JavaScript from AppData\Roaming, a known Gootloader persistence indicator. This motivated building a behavior-based, multi-stage detection.

The query correlates:

Execution Phase
1. JavaScript launched through wscript.exe or cscript.exe
2. PowerShell spawned from script hosts
3. PowerShell performing Gootloader-specific reconnaissance
(environment variables, OS info, window title collection)

Persistence Phase
1. Scheduled tasks running .js from AppData\Roaming
2. Startup folder .lnk persistence
3. wscript/cscript executing 8.3-style JavaScript from AppData (EMCCON~1.JS)

The detection fires only when both phases occur, drastically reducing false positives.


// Gootloader / Vanilla Tempest combined detection
// Triggers when execution (JS -> PowerShell recon) AND
// persistence (task or Startup/8.3 JS) both occur on the same device within 24 hours.
// =====================
// 1. EXECUTION PHASE
// =====================
let ExecEvents =
    DeviceProcessEvents
    | where Timestamp > ago(30d)
    // PowerShell spawned by wscript/cscript
    | where FileName =~ "powershell.exe"
    | where InitiatingProcessFileName in~ ("wscript.exe", "cscript.exe")
    // Heuristic Gootloader recon pattern in *command line*
//   (no script block logging in this environment)
    | where ProcessCommandLine has "Get-ChildItem"
      and ProcessCommandLine has "env:"
      and ProcessCommandLine has ".value -match"
      and ProcessCommandLine has "Get-CimInstance"
      and ProcessCommandLine has "Win32_OperatingSystem"
      and ProcessCommandLine has "Get-Process"
      and ProcessCommandLine has "ConvertTo-Csv"
    | project
        DeviceId,
        DeviceName,
        AccountName,
        ExecTime        = Timestamp,
        ExecType        = "JS_to_PS_Recon",
        ExecCommandLine = ProcessCommandLine,
        ExecParent      = InitiatingProcessFileName,
        ExecParentCmd   = InitiatingProcessCommandLine;
// =====================
// 2. PERSISTENCE PHASE
// =====================
// 2a. Scheduled tasks that run JS from AppData\Roaming via wscript/cscript
let TaskJsPersist =
    DeviceEvents
    | where Timestamp > ago(7d)
    | where ActionType in ("ScheduledTaskCreated",
                           "ScheduledTaskRegistered",
                           "ScheduledTaskUpdated")
    | extend TaskContent = tostring(parse_json(AdditionalFields).TaskContent)
    | where TaskContent has ".js"
      and TaskContent has @"\AppData\Roaming\"
      and (TaskContent has "wscript.exe" or TaskContent has "cscript.exe")
    | project
        DeviceId,
        DeviceName,
        AccountName,
        PersistTime   = Timestamp,
        PersistenceType = "ScheduledTask_JS_AppData",
        PersistenceDetails = TaskContent;
// 2b. LNK files dropped in Startup folder
let StartupLnkPersist =
    DeviceFileEvents
    | where Timestamp > ago(7d)
    | where FileName endswith ".lnk"
    | where FolderPath has @"\Microsoft\Windows\Start Menu\Programs\Startup"
    | project
        DeviceId,
        DeviceName,
        InitiatingProcessAccountName,
        PersistTime   = Timestamp,
        PersistenceType = "Startup_LNK",
        PersistenceDetails = strcat(
            FolderPath, "\\", FileName,
            " | CreatedBy=", InitiatingProcessFileName, " ", InitiatingProcessCommandLine
        );
// 2c. wscript/cscript executing 8.3 JS from AppData\Roaming
let ShortNameJsExec =
    DeviceProcessEvents
    | where Timestamp > ago(7d)
    | where FileName in~ ("wscript.exe", "cscript.exe")
    | where ProcessCommandLine has ".js"
      and ProcessCommandLine has @"\AppData\Roaming\"
      // 8.3 short name pattern: tilde + digits before .js
      and ProcessCommandLine matches regex @"~\d+\.js(?i)"
    | project
        DeviceId,
        DeviceName,
        AccountName,
        PersistTime   = Timestamp,
        PersistenceType = "JS_8dot3_AppData_Exec",
        PersistenceDetails = ProcessCommandLine;
// Union all persistence indicators
let PersistenceEvents =
    TaskJsPersist
    | union StartupLnkPersist
    | union ShortNameJsExec;
// =====================
// 3. CORRELATION (24h)
// =====================
ExecEvents
| join kind=inner PersistenceEvents on DeviceId
| where PersistTime between (ExecTime .. ExecTime + 24h)
| project
    DeviceName,
    AccountName,
    ExecTime,
    PersistTime,
    TimeDeltaHours = datetime_diff("hour", PersistTime, ExecTime),
    ExecType,
    PersistenceType,
    ExecParent,
    ExecParentCmd,
    ExecCommandLine,
    PersistenceDetails
| order by ExecTime desc
